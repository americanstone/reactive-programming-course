buildscript {
    repositories {
        mavenCentral()
    }
}

def printOutput(def output) {
    return tasks.create("printOutput") {
        for (line in output.toString().readLines()) {
            println "#educational_plugin" + line
        }
    }
}

subprojects {
    apply plugin: 'application'
    apply plugin: 'java'

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/release" }
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }

    ext {
        junitBomVersion='5.5.2'
        reactorBomVersion='Dysprosium-SR4'
        r2dbcBomVersion='Arabba-SR2'
        micrometerBomVersion='1.3.5'

        logbackVersion='1.2.3'
        assertjVersion='3.15.0'
        mockitoVersion='3.2.0'
        jacksonVersion='2.10.2'

        mongoReactiveDriverVersion='1.13.0'
        socketIOVersion='1.0.0'
        embeddedMongoVersion='2.2.0'
    }

    dependencies {
        // BOMs defenition
        compile platform('io.projectreactor:reactor-bom:' + reactorBomVersion)
        compile platform('io.r2dbc:r2dbc-bom:' + r2dbcBomVersion)
        compile platform('io.micrometer:micrometer-bom:' + micrometerBomVersion)
        
        compile 'ch.qos.logback:logback-core:' + logbackVersion
        compile 'ch.qos.logback:logback-classic:' + logbackVersion

        testCompile platform('org.junit:junit-bom:' + junitBomVersion)
        testCompile platform('io.projectreactor:reactor-bom:' + reactorBomVersion)
        testCompile platform('io.r2dbc:r2dbc-bom:' + r2dbcBomVersion)

        // general dependencies for every project
        compile "io.projectreactor:reactor-core"

        testCompile 'ch.qos.logback:logback-core:' + logbackVersion
        testCompile 'ch.qos.logback:logback-classic:' + logbackVersion
        testCompile 'org.assertj:assertj-core:' + assertjVersion
        testCompile 'org.mockito:mockito-core:' + mockitoVersion
        testCompile 'io.projectreactor:reactor-test'
        testCompile 'org.junit.jupiter:junit-jupiter-api'

        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
    }

    sourceSets {
        main {
            java.srcDir 'src'
            resources.srcDirs 'resources'
        }
        test {
            java.srcDir 'test'
            resources.srcDirs 'resources'
        }
        resources
    }

    test {
        useJUnitPlatform()
    }

    mainClassName = project.hasProperty("mainClass") ? project.getProperty("mainClass") : ""

    def runOutput = new ByteArrayOutputStream()
    tasks.run.setStandardOutput(runOutput)
    tasks.run.doLast { printOutput(runOutput) }

    task cleanBuildDir(type: Delete) {
        delete "${projectDir}/out"
    }
    tasks.clean.dependsOn(cleanBuildDir)
}

project(':util') {
    dependencies {
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.5.2")
    }
}

configure(subprojects.findAll {it.name != 'util'}) {
  dependencies {
    compile project(':util').sourceSets.main.output
    testCompile project(':util').sourceSets.test.output
  }
}
